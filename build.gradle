plugins {
    id 'java'
    id 'application'
    id 'com.gradleup.shadow' version '9.2.2'
}

group = 'io.github.qpcrummer'
version = '2.0.1'

repositories {
    mavenCentral()
}

def osName = System.getProperty("os.name").toLowerCase()

// Allow manual override: -Pplatform=windows|linux|macos
def platform = project.findProperty("platform")

def resolvedPlatform
if (platform != null) {
    resolvedPlatform = ((String) platform).toLowerCase()
} else if (osName.contains("win")) {
    resolvedPlatform = "windows"
} else if (osName.contains("linux")) {
    resolvedPlatform = "linux"
} else if (osName.contains("mac")) {
    resolvedPlatform = "macos"
} else {
    throw new GradleException("Unsupported OS: " + osName)
}

dependencies {
    implementation 'org.xerial:sqlite-jdbc:3.51.0.0'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'org.slf4j:slf4j-simple:2.0.17'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'org.apache.pdfbox:pdfbox:3.0.6'
    implementation 'net.lingala.zip4j:zip4j:2.11.5'
    implementation("io.qtjambi:qtjambi:6.4.2")

    if (resolvedPlatform == "windows") {
        implementation("io.qtjambi:qtjambi-native-windows-x64:6.4.2")
    } else if (resolvedPlatform == "linux") {
        implementation("io.qtjambi:qtjambi-native-linux-x64:6.4.2")
    } else if (resolvedPlatform == "macos") {
        implementation("io.qtjambi:qtjambi-native-macos:6.4.2")
    }
}

tasks.shadowJar {
    if (platform != null) {
        archiveBaseName = "Spool-" + platform
    } else if (osName.contains("win")) {
        archiveBaseName = "Spool-Windows"
    } else if (osName.contains("linux")) {
        archiveBaseName = "Spool-Linux"
    } else if (osName.contains("mac")) {
        archiveBaseName = "Spool-macOS"
    } else {
        throw new GradleException("Unsupported OS: " + osName)
    }
}

application {
    mainClass = 'io.github.qpcrummer.spool.Main'
}

def targetJavaVersion = 25
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}
